(export #t)
;; Simple extensible vectors, byte-vectors, and bit-vectors
;; They double their size each time they need to grow,
;; or you can insist on controlling what size they grow to.

(import
  :gerbil/gambit
  :std/misc/bytes
  :std/misc/number
  :std/srfi/43
  :std/error
  :std/sugar)

(defstruct evector (values fill-pointer) transparent: #t unchecked: #t)
(def (vector->evector v) (make-evector v (vector-length v)))
(def (list->evector l) (vector->evector (list->vector l)))
(def (new-evector length: l initial-value: (v (void)) fill-pointer: (p l))
  (make-evector (make-vector (max l p) v) p))
(def (evector-ref v i)
  (check-argument (< i (evector-fill-pointer v)) "index less than fill-pointer" i)
  (vector-ref (&evector-values v) i))
(def (evector-set! v i x)
  (check-argument (< i (evector-fill-pointer v)) "index less than fill-pointer" i)
  (vector-set! (&evector-values v) i x))
(def evector-ref-set! evector-set!)
(def (extend-evector! e ll initial-value: (iv #f))
  (def v (evector-values e))
  (when (> ll (vector-length v))
    (set! (&evector-values e) (vector-copy v 0 ll iv))))
(def (evector-set-fill-pointer! e fp initial-value: (iv #f) extend: (extend #f))
  (def l (vector-length (evector-values e)))
  (let/cc return
    (when (> fp l)
      (unless (or (eq? extend #t) (and (exact-integer? extend) (< 0 extend)))
        (return #f))
      (extend-evector! e (if (eq? extend #t)
                           (arithmetic-shift 1 (max 4 (integer-length fp)))
                           (+ extend fp))
                       initial-value: iv))
    (set! (&evector-fill-pointer e) fp)
    fp))
(def (evector-push! e x initial-value: (iv #f) extend: (extend #f))
  (def i (evector-fill-pointer e))
  (and (evector-set-fill-pointer! e (1+ i) initial-value: iv extend: extend)
       (begin
         (vector-set! (&evector-values e) i x)
         i)))
(def (evector->vector e)
  (vector-copy (evector-values e) 0 (evector-fill-pointer e)))
(def (evector->list l) (vector->list (evector->vector l)))

;;; memoize the start of a recursively defined sequence
(def (memoize-recursive-sequence fun cache: (cache (list->evector '())))
  (lambda (n)
    (check-argument (nat? n) "natural integer" n)
    (def m (evector-fill-pointer cache))
    (if (< n m)
      (vector-ref (&evector-values cache) n)
      (begin
        (evector-set-fill-pointer! cache (1+ n) initial-value: (void) extend: #t)
        (let loop ((i m))
          (def v (fun i))
          (vector-set! (&evector-values cache) i v)
          (if (= i n) v (loop (1+ i))))))))

(defstruct ebytes (bytes fill-pointer) transparent: #t)
(def (bytes->ebytes b) (make-ebytes b (u8vector-length b)))
(def (string->ebytes s) (bytes->ebytes (string->bytes s)))
(def (new-ebytes length: l initial-value: (v 0) fill-pointer: (p l))
  (make-ebytes (make-u8vector (max l p) v) p))
(def (ebytes-ref e i) (u8vector-ref (ebytes-bytes e) i))
(def (ebytes-set! e i x) (u8vector-set! (ebytes-bytes e) i x))
(def ebytes-ref-set! ebytes-set!)
(def (extend-ebytes! e ll initial-value: (iv 0))
  (def b (ebytes-bytes e))
  (when (> ll (u8vector-length b))
    (let (bb (make-u8vector ll iv))
      (set! (ebytes-bytes e) bb)
      (subu8vector-move! b 0 (ebytes-fill-pointer e) bb 0))))
(def (ebytes-set-fill-pointer! e fp initial-value: (iv 0) extend: (extend #f))
  (def l (u8vector-length (ebytes-bytes e)))
  (let/cc return
    (when (> fp l)
      (unless (or (eq? extend #t) (and (exact-integer? extend) (< 0 extend)))
        (return #f))
      (extend-ebytes! e (if (eq? extend #t)
                         (arithmetic-shift 1 (max 4 (integer-length fp)))
                         (+ extend fp))
                     initial-value: iv))
    (set! (ebytes-fill-pointer e) fp)
    fp))
(def (ebytes-push! e x initial-value: (iv 0) extend: (extend #f))
  (def i (ebytes-fill-pointer e))
  (def bb (cond
           ((u8vector? x) x)
           ((exact-integer? x) (make-u8vector 1 x))
           ((string? x) (string->bytes x))))
  (def ll (u8vector-length bb))
  (and (ebytes-set-fill-pointer! e (+ i ll) initial-value: iv extend: extend)
       (begin
         (subu8vector-move! bb 0 ll (ebytes-bytes e) i)
         i)))
(def (ebytes->bytes e)
  (subu8vector (ebytes-bytes e) 0 (ebytes-fill-pointer e)))

;; Bytes as extensible bit vectors in little-endian way
(defstruct ebits (bits fill-pointer) transparent: #t)
(def (bits->ebits b l)
  (def ll (n-bits->n-u8 l))
  (def bb (make-u8vector ll 0))
  (u8vector-uint-set! bb 0 b little ll)
  (make-ebits bb l))
(def (new-ebits length: l initial-value: (b 0) fill-pointer: (p l))
  (make-ebits (make-u8vector (n-bits->n-u8 (max l p)) (if (zero? b) b 255)) p))
(def (ebits-set? e i)
  (bit-set? (bitwise-and i 7) (u8vector-ref (ebits-bits e) (arithmetic-shift i -3))))
(def (ebits-ref e i) (if (ebits-set? e i) 1 0))
(def (ebits-set! e i x)
  (def ii (arithmetic-shift i -3))
  (def bit (arithmetic-shift 1 (bitwise-and i 7)))
  (def b (ebits-bits e))
  (def y (u8vector-ref b ii))
  (u8vector-set! b ii (if (zero? x) (bitwise-and y (bitwise-not bit)) (bitwise-ior y bit))))
(def ebits-ref-set! ebits-set!)
(def (extend-ebits! e ll initial-value: (iv 0))
  (def b (ebits-bits e))
  (def bl (n-bits->n-u8 ll))
  (when (> bl (u8vector-length b))
    (let (bb (make-u8vector bl (if (zero? iv) 0 255)))
      (set! (ebits-bits e) bb)
      (subu8vector-move! b 0 (n-bits->n-u8 (ebits-fill-pointer e)) bb 0))))
(def (ebits-set-fill-pointer! e fp initial-value: (iv 0) extend: (extend #f))
  (def l (u8vector-length (ebits-bits e)))
  (def bl (n-bits->n-u8 fp))
  (let/cc return
    (when (> bl l)
      (unless (or (eq? extend #t) (and (exact-integer? extend) (< 0 extend)))
        (return #f))
      (extend-ebits! e (if (eq? extend #t)
                         (arithmetic-shift 1 (max 6 (integer-length fp)))
                         (+ extend fp))
                     initial-value: iv))
    (set! (ebits-fill-pointer e) fp)
    fp))
(def (ebits-push! e x initial-value: (iv 0) extend: (extend #f))
  (def i (ebits-fill-pointer e))
  (and (ebits-set-fill-pointer! e (1+ i) initial-value: iv extend: extend)
       (begin
         (ebits-set! e i x)
         i)))
(def (ebits->bits e)
  (def l (ebits-fill-pointer e))
  (values (clear-bit-field 8 l (u8vector-uint-ref (ebits-bits e) 0 little (n-bits->n-u8 l))) l))
